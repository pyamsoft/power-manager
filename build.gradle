/*
 * Copyright 2013 - 2016 Peter Kenji Yamanaka
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

tasks.whenTaskAdded { task ->
  if (task.name.startsWith("lint")) {
    task.enabled = false
  }
}

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    // Retro lambda
    classpath 'me.tatarka:gradle-retrolambda:3.3.0-beta4'

    // Dex count
    classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.2'

    // Android APT
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'

    // Gradle version checked
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'me.tatarka.retrolambda'

android {

  compileSdkVersion 23
  buildToolsVersion '23.0.3'

  defaultConfig {
    applicationId "com.pyamsoft.powermanager"
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 43
    versionName '6.0.0'

    // For vector drawables
    vectorDrawables.useSupportLibrary = true

    // For instrumentation test
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  signingConfigs {
    release {
      storeFile new File(RELEASE_STORE_FILE)
      keyAlias RELEASE_KEY_ALIAS
      keyPassword RELEASE_KEY_PASSWD
      storePassword RELEASE_STORE_PASSWD
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
    }
  }

  lintOptions {
    disable 'InvalidPackage'
  }

  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
}

android.applicationVariants.all { variant ->
  if (variant.buildType.name == 'release') {
    variant.outputs.each { output ->
      output.assemble.doLast {
        println "Zopflifying... it might take a while"
        exec {
          commandLine output.zipAlign.zipAlignExe, '-f', '-z', '4', output.outputFile.absolutePath,
              output.outputFile.absolutePath.replaceAll('\\.apk$', '-zopfli.apk')
        }
      }
    }
  }
}

retrolambda {
  jvmArgs '-noverify'
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:2.3.0'

  // JavaPoet to fix an issue
  apt 'com.squareup:javapoet:1.7.0'

  // SQLBrite
  compile 'com.squareup.sqlbrite:sqlbrite:0.6.3'

  // RxJava
  // rxandroid pulls in sometimes old rxjava
  compile 'io.reactivex:rxjava:1.1.5'
  compile 'io.reactivex:rxandroid:1.2.0'
  compile 'com.artemzin.rxjava:proguard-rules:1.1.5.0'

  // Auto value
  provided 'com.google.auto.value:auto-value:1.2'
  apt 'com.google.auto.value:auto-value:1.2'
  apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.2'

  // Dagger2
  // Dagger 2 and Compiler
  compile 'com.google.dagger:dagger:2.4'
  apt "com.google.dagger:dagger-compiler:2.4"

  // Butterknife
  compile 'com.jakewharton:butterknife:8.0.1'
  apt 'com.jakewharton:butterknife-compiler:8.0.1'

  // Showcase View
  compile 'com.github.deano2390:MaterialShowcaseView:1.1.0'

  // Android Framework Testing
  androidTestCompile 'com.android.support:support-annotations:23.4.0'
  androidTestCompile 'com.android.support.test:runner:0.5'
  androidTestCompile 'com.android.support.test:rules:0.5'
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'

  // Roboelectric for Android on JVM
  testCompile "org.robolectric:robolectric:3.1"

  // JUnit for unit tests
  testCompile 'junit:junit:4.12'

  // Mockito for unit tests
  testCompile 'org.mockito:mockito-core:2.0.54-beta'

  // PYDroid 1.8.2
  compile project(':PYDroid')
}
